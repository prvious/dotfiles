#!/bin/zsh

# Docker devcontainer proxy - run commands in container
# Usage: x [command]     - run command in container
#        x               - open shell in container
#        x up|down|stop  - manage container lifecycle

set -e

error() { echo "âœ— $1" >&2; exit 1; }

devcontainer="$HOME/.local/bin/devcontainer-insiders"

# Subcommands
if [[ $# -eq 1 ]]; then
    case "$1" in
        code|open) exec "$devcontainer" open ;;
        up)        exec "$devcontainer" up ;;
        build)     exec "$devcontainer" build ;;
        down)      exec docker compose down ;;
        stop)      exec docker compose stop ;;
        rm)        exec docker compose rm ;;
    esac
fi

# Validate
config=".devcontainer/devcontainer.json"
[[ -f "$config" ]] || error "No $config found"
command -v bun &>/dev/null || error "bun required"

# Parse config with bun
read -r service user < <(bun -e "
    const text = await Bun.file('$config').text();
    const c = Bun.JSONC.parse(text);
    console.log(c.service ?? '', c.remoteUser ?? '');
")

[[ -n "$service" ]] || error "No 'service' in $config"

# Build and execute
cmd=(docker compose exec)
[[ -n "$user" ]] && cmd+=(-u "$user")
cmd+=("$service")

if [[ $# -eq 0 ]]; then
    "${cmd[@]}" zsh
else
    "${cmd[@]}" zsh -ic "$*"
fi
